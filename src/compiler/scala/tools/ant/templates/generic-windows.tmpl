@@echo off

rem ##########################################################################
rem # Copyright @copyright@
rem #
rem # This is free software; see the distribution for copying conditions.
rem # There is NO warranty; not even for MERCHANTABILITY or FITNESS FOR A
rem # PARTICULAR PURPOSE.
rem ##########################################################################

rem We assume the following conventions:
rem - System/user environment variables start with a letter
rem - Local batch variables start with a '_'

if "%OS%"=="Windows_NT" (
  @@setlocal
  set _SCALA_HOME=%~dp0..
) else (
  set _SCALA_HOME=%SCALA_HOME%
  if "%_SCALA_HOME%"=="" goto error1
)

set _JAVACMD=%JAVACMD%
if "%_JAVACMD%"=="" set _JAVACMD=java
set _JAVAFLAGS=@javaflags@

set _CPATH=.
set _CLASSPATH=@classpath@
if not "%_CLASSPATH%"=="" goto args
for %%f in ("%_SCALA_HOME%\lib\*.jar") do call :add_cpath "%%f"

:args
set _ARGS=
:loop
if "%1"=="" goto exec
if "%1"=="-classpath" goto cpath
if "%1"=="-cp" goto cpath
if "%1"=="-version" goto version
set _ARGS=%_ARGS% %1
shift
goto loop

:cpath
shift
if "%1"=="" goto help
set _CPATH=%1
shift
goto :loop

:exec
set _CLASSPATH=%_CPATH%;%_CLASSPATH%
rem echo %_JAVACMD% %_JAVAFLAGS% -cp %_CLASSPATH% %_ARGS%
%_JAVACMD% %_JAVAFLAGS% -cp %_CLASSPATH% %_ARGS%
goto end

:version
echo Scala runtime version @version@ -- @copyright@
%_JAVACMD% -version
goto end

:help
echo Scala runtime version @version@ -- @copyright@
%_JAVACMD% -help
goto end

rem ##########################################################################
rem # subroutines

:add_cpath
  if "%_CLASSPATH%"=="" (
    set _CLASSPATH=%~1
  ) else (
    set _CLASSPATH=%_CLASSPATH%;%~1
  )
goto :eof

rem ##########################################################################
rem # errors

:error1
echo ERROR: environment variable SCALA_HOME is undefined. It should point to your installation directory.
goto end

:end
if "%OS%"=="Windows_NT" @@endlocal
