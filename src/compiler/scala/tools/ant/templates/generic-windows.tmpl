@@echo off

rem ##########################################################################
rem # Copyright @copyright@
rem #
rem # This is free software; see the distribution for copying conditions.
rem # There is NO warranty; not even for MERCHANTABILITY or FITNESS FOR A
rem # PARTICULAR PURPOSE.
rem ##########################################################################

rem We adopt the following conventions:
rem - System/user environment variables start with a letter
rem - Local batch variables start with an underscore ('_')

if "%OS%"=="Windows_NT" (
  @@setlocal
  set _SCALA_HOME=%~dps0..
) else (
  set _SCALA_HOME=%SCALA_HOME%
  if "%_SCALA_HOME%"=="" goto error1
)

rem We use the value of the JAVACMD environment variable if defined
set _JAVACMD=%JAVACMD%
if "%_JAVACMD%"=="" set _JAVACMD=java

set _EXTENSION_CLASSPATH=@extclasspath@
set _BOOT_CLASSPATH=

rem We use the value of the CLASSPATH environment variable if defined
set _USER_CLASSPATH=%CLASSPATH%
if "%_USER_CLASSPATH%"=="" set _USER_CLASSPATH=.

set _CLASSPATH=@classpath@
if not "%_CLASSPATH%"=="" goto args
for %%f in ("%_SCALA_HOME%\lib\*.jar") do call :add_cpath "%%f"

:args
set _ARGS=
:loop
if "%1"=="" goto exec
if "%1"=="-classpath" goto cpath
if "%1"=="-cp" goto cpath
if "%1"=="-version" goto version
set _ARGS=%_ARGS% %1
shift
goto loop

:cpath
shift
if "%1"=="" goto help
set _USER_CLASSPATH=%1
shift
goto :loop

:exec
set _PROPS=-Dscala.home="%_SCALA_HOME%" -Dscala.boot.class.path="%_BOOT_CLASSPATH%" -Dscala.class.path="%_CLASSPATH%" -Dscala.ext.path="%_EXTENSION_CLASSPATH%" -Denv.classpath="%CLASSPATH%" @properties@

set _CLASSPATH=%_USER_CLASSPATH%;%_CLASSPATH%
rem echo %_JAVACMD% @javaflags@ %_PROPS% -cp %_CLASSPATH% %_ARGS%
%_JAVACMD% @javaflags@ %_PROPS% -cp %_CLASSPATH% %_ARGS%
goto end

:version
echo Scala runtime version @version@ -- @copyright@
%_JAVACMD% -version
goto end

:help
echo Scala runtime version @version@ -- @copyright@
%_JAVACMD% -help
goto end

rem ##########################################################################
rem # subroutines

:add_cpath
  if "%_CLASSPATH%"=="" (
    set _CLASSPATH=%~1
  ) else (
    set _CLASSPATH=%_CLASSPATH%;%~1
  )
goto :eof

rem ##########################################################################
rem # errors

:error1
echo ERROR: environment variable SCALA_HOME is undefined. It should point to your installation directory.
goto end

:end
if "%OS%"=="Windows_NT" @@endlocal
